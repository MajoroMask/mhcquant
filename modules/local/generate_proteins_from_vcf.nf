process GENERATE_PROTEINS_FROM_VCF {
    tag "$meta"
    label 'process_medium'

    conda (params.enable_conda ? "bioconda::fred2=2.0.6 bioconda::mhcflurry=1.4.3 bioconda::mhcnuggets=2.3.2" : null)
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/mulled-v2-689ae0756dd82c61400782baaa8a7a1c2289930d:a9e10ca22d4cbcabf6b54f0fb5d766ea16bb171e-0' :
        'quay.io/biocontainers/mulled-v2-689ae0756dd82c61400782baaa8a7a1c2289930d:a9e10ca22d4cbcabf6b54f0fb5d766ea16bb171e-0' }"

    input:
        tuple val(meta), path(fasta), path(vcf)

    output:
        tuple val(meta), path("*.fasta"), emit: vcf_fasta
        path "versions.yml"           , emit: versions

    script:
        def prefix           = task.ext.prefix ?: "${fasta.baseName}_added_vcf"
        def args             = task.ext.args  ?: ''

        """
        variants2fasta.py -v $vcf \\
            -f $fasta \\
            -o ${meta.sample}_${prefix}.fasta \\
            $args

        cat <<-END_VERSIONS > versions.yml
        "${task.process}":
            fred2: \$(echo \$(python -c "import pkg_resources; print('fred2' + pkg_resources.get_distribution('Fred2').version)" | sed 's/^fred2//; s/ .*\$//'))
            mhcnuggets: \$(echo \$(python -c "import pkg_resources; print('mhcnuggets' + pkg_resources.get_distribution('mhcnuggets').version)" | sed 's/^mhcnuggets//; s/ .*\$//' ))
            mhcflurry: \$(echo \$(mhcflurry-predict --version 2>&1 | sed 's/^mhcflurry //; s/ .*\$//') )
        END_VERSIONS
        """
}
